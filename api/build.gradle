plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
    id 'dev.obscuria.hekate'
}

dependencies {
    // Mixins
    compileOnly('org.spongepowered:mixin:0.8.5')
    compileOnly('io.github.llamalad7:mixinextras-common:0.3.5')
    annotationProcessor('io.github.llamalad7:mixinextras-common:0.3.5')
    // Configs
    api("fuzs.forgeconfigapiport:forgeconfigapiport-common-neoforgeapi:${config_version}")
}

neoForge {
    neoFormVersion = neo_form_version
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

hekate {
    metadata = uri(metadata_lookup)
    shouldPublishToCurseForge = false
    shouldPublishToModrinth = false
}

